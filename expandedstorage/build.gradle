plugins {
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

dependencies {
    compile project(path: ":ninjaphenix-container-library"), withoutUnwanted
    compile project(path: ":chainmail"), withoutUnwanted
    compile project(path: ":creativebuttonmover"), withoutUnwanted

    include project(path: ":ninjaphenix-container-library")
    include project(path: ":chainmail")
    include project(path: ":creativebuttonmover")
    include "io.github.cottonmc:Jankson-Fabric:${jankson_version}"

    modCompileOnly("virtuoel:Towelette:${towelette_version}") {
        exclude(group: "com.github.Virtuoel", module: "Statement")
    }
    modCompileOnly("virtuoel:Statement:${statement_version}")
}

if (project.hasProperty("api_key")) {
    curseforge {
        apiKey = project.api_key
        //noinspection GroovyAssignabilityCheck
        project {
            id = "317856"
            changelogType = "markdown"
            changelog = file("${project.projectDir}/changelog.md")
            releaseType = "${project.cf_release_type}"
            addGameVersion "${project.cf_game_version}"
            addGameVersion "Fabric"
            addGameVersion "Java 8"
            relations {
                requiredDependency "fabric-api"
                embeddedLibrary "jankson"
                optionalDependency "modmenu"
                optionalDependency "roughly-enough-items"
                optionalDependency "towelette"
            }
            mainArtifact(file("${project.buildDir}/libs/${mod_id}-${mod_version}+${minecraft_version}.jar")) {
                displayName = "Expanded Storage ${mod_version}+${minecraft_version}"
            }
            addArtifact(file("${project.buildDir}/libs/${mod_id}-${mod_version}+${minecraft_version}-sources.jar"))
            afterEvaluate {
                uploadTask.dependsOn("remapSourcesJar")
            }
        }
        options {
            forgeGradleIntegration = false
        }
    }
    tagRelease {
        dependsOn "curseforge"
    }
}